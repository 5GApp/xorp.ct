# Copyright (c) 2009 XORP, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, Version 2, June
# 1991 as published by the Free Software Foundation. Redistribution
# and/or modification of this program under the terms of any other
# version of the GNU General Public License is not permitted.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. For more details,
# see the GNU General Public License, Version 2, a copy of which can be
# found in the XORP LICENSE.gpl file.
#
# XORP Inc, 2953 Bunker Hill Lane, Suite 204, Santa Clara, CA 95054, USA;
# http://xorp.net

# $XORP$

# FIXME: tests

import os
Import('env')

env = env.Clone()

is_shared = env.has_key('SHAREDLIBS')

subdirs = ['tools']

SConscript(dirs = subdirs, exports = 'env')

env.AppendUnique(CPPPATH = [
    '#',
    '$BUILDDIR',
    ])

env.AppendUnique(LIBPATH = [
    '$BUILDDIR/libxorp',
    '$BUILDDIR/libcomm',
    '$BUILDDIR/libxipc',
    '$BUILDDIR/libproto',
    '$BUILDDIR/libfeaclient',
    '$BUILDDIR/policy/backend',
    '$BUILDDIR/policy/common',
    '$BUILDDIR/xrl/interfaces',
    '$BUILDDIR/xrl/targets',
    '$BUILDDIR/mrt',
    '.'
    ])

env.AppendUnique(LIBS = [
    'olsr',
    'olsr4base',
    'policybackend',
    'policycommon',
    'xorp_fea_client',
    'ribxif',
    'findereventnotifierxif',
    'feaifmgrmirrorxif',
    'feaifmgrreplicatorxif',
    'profileclientxif',
    'fearawpkt4xif',
    'fearawpkt6xif',
    'feaifmgrmirrorbase',
    'xorp_ipc',
    'xorp_core',
    'xorp_proto',
    'xorp_comm',
])

### libolsr

libolsrsrcs = [
	'external.cc',
	'face.cc',
	'face_manager.cc',
	'link.cc',
	'message.cc',
	'neighbor.cc',
	'neighborhood.cc',
	'olsr.cc',
	'olsr_types.cc',
	'policy_varrw.cc',
	'route_manager.cc',
	'topology.cc',
	'twohop.cc',
	]


if is_shared:
    libolsr = env.SharedLibrary(target = 'libolsr', source = libolsrsrcs, LIBS = '')
    env.Alias('install',
              env.InstallLibrary('$libdir', libolsr))
else:
    libolsr = env.StaticLibrary(target = 'libolsr', source = libolsrsrcs, LIBS = '')

### libolsrxrl

libolsrxrlsrcs = [
	'xrl_io.cc',
	'xrl_port.cc',
	'xrl_queue.cc',
	'xrl_target.cc',
	]

if is_shared:
    libolsrxrl = env.SharedLibrary(target = 'libolsrxrl', source = libolsrxrlsrcs, LIBS = '')
    env.Alias('install',
              env.InstallLibrary('$libdir', libolsrxrl))
else:
    libolsrxrl = env.StaticLibrary(target = 'libolsrxrl', source = libolsrxrlsrcs, LIBS = '')

### olsr4

olsr4srcs = [
	'xorp_olsr.cc',
	]

olsr4 = env.Program(target = 'xorp_olsr4', source = olsr4srcs)

# XXX not ready for prime time.

env.Alias('install',
          env.InstallProgram('$bindir', olsr4))

Default(libolsr, libolsrxrl, olsr4)
