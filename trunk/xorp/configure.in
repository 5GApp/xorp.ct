dnl -*- sh -*-
dnl
dnl $XORP: xorp/configure.in,v 1.136 2005/06/03 22:30:42 pavlin Exp $
dnl
dnl ---------------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl ---------------------------------------------------------------------------
dnl
dnl	Ordering:
dnl	  Autoconf setup
dnl	  Autoheader setup
dnl	  Automake setup
dnl	  Installation prefixes
dnl	  Operating system probing
dnl	  Toolchain probing
dnl	  Libtool setup
dnl	  Command-line options
dnl	  Additional tool probing
dnl	  Check for libraries
dnl	  Check for header files and types
dnl	  Check for typedefs and structures
dnl	  Check for library functions
dnl	  Check for system services
dnl	  Check for compiler characteristics
dnl	  Setup compiler flags
dnl	  Output (e.g., 'AC_OUTPUT([FILE...])', etc.)
dnl
dnl To disable config.cache loading and saving, redefine the following two
dnl macros _before_ calling AC_INIT, but make sure to keep the trailing dnl.
dnl
dnl define([AC_CACHE_LOAD], )dnl
dnl define([AC_CACHE_SAVE], )dnl

dnl ---------------------------------------------------------------------------
dnl Boilerplate
dnl ---------------------------------------------------------------------------

AC_INIT(xorp, [1.1], [Report configure problems to feedback@xorp.org])
AC_PREREQ(2.53)
AC_REVISION([$XORP: xorp/configure.in,v 1.136 2005/06/03 22:30:42 pavlin Exp $])
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_TARGET
AC_CONFIG_SUBDIRS(cli/libtecla)

dnl XXX: We can't use AC_CONFIG_HEADERS yet because automake is old.
dnl builtin(include, config/ahxorp.m4)
dnl AC_CONFIG_HEADERS(config.h)

dnl ----------------------------
dnl automake options
dnl ----------------------------
AM_INIT_AUTOMAKE(xorp, 1.1)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AC_CACHE_SAVE

dnl
dnl Change the default installation prefix
dnl
AC_PREFIX_DEFAULT(/usr/local/xorp)
test "$prefix" = NONE && prefix="${ac_default_prefix}"
AC_DEFINE_UNQUOTED(XORP_ROOT, "${prefix}",
		   [Define the path to the root of the XORP installation])

AC_DEFINE_UNQUOTED(XORP_INSTALL_ROOT, "${prefix}",
		   [Define the path to the root of the XORP installation])

AC_DEFINE_UNQUOTED(XORP_BUILD_ROOT, "${PWD}",
    		   [Define path to the top-level build directory])

# We'd like to use ac_abs_srcdir, except it's not available just here.
AC_DEFINE_UNQUOTED(XORP_SRC_ROOT, "`cd ${srcdir} && echo ${PWD}`",
    		   [Define path to the top-level source directory])

dnl ----------------------------
dnl Set host, host_alias, host_cpu, host_vendor, host_os
dnl ----------------------------

case "${host_os}" in
    bsdi* )
	AC_DEFINE(HOST_OS_BSDI, 1, [Define to 1 if the OS is BSDI])
    ;;
    darwin* )
	AC_DEFINE(HOST_OS_MACOSX, 1, [Define to 1 if the OS is MacOS X])
    ;;
    freebsd* )
	AC_DEFINE(HOST_OS_FREEBSD, 1, [Define to 1 if the OS is FreeBSD])
    ;;
    linux* )
	AC_DEFINE(HOST_OS_LINUX, 1, [Define to 1 if the OS is Linux])
    ;;
    netbsd* )
	AC_DEFINE(HOST_OS_NETBSD, 1, [Define to 1 if the OS is NetBSD])
    ;;
    openbsd* )
	AC_DEFINE(HOST_OS_OPENBSD, 1, [Define to 1 if the OS is OpenBSD])
    ;;
    solaris* )
	AC_DEFINE(HOST_OS_SOLARIS, 1, [Define to 1 if the OS is Solaris])
    ;;
    *cygwin )
	AC_MSG_ERROR([Cygwin is not (and will not be) supported.])
    ;;
    *mingw* )
	AC_DEFINE(HOST_OS_WINDOWS, 1, [Define to 1 if the OS is Windows])
	AC_DEFINE(HAVE_MINGW, 1, [Define to 1 if the build environment is MinGW])
	_USING_WINDOWS="1"
	_USING_MINGW="1"
	dnl This has to be here to stop the ssize_t test from telling lies.
	CPPFLAGS="${CPPFLAGS} -D_NO_OLDNAMES"
    ;;
esac
AC_CACHE_SAVE

dnl ---------------------------------------------------------------------------
dnl Compiler probing and basic tools (libtool prerequisites)
dnl ---------------------------------------------------------------------------

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_LN_S

AC_LANG(C++)

AC_CACHE_SAVE

dnl
dnl Include the macros for testing compiler-supported command-line options
dnl
builtin(include, config/compiler_flags.m4)

dnl ----------------------------
dnl  Get project-specific macros
dnl ----------------------------

builtin(include, config/acxorp.m4)

dnl ----------------------------
dnl libtool options
dnl ----------------------------
AC_DISABLE_SHARED
AC_DISABLE_FAST_INSTALL
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
AC_CACHE_SAVE

dnl ---------------------------------------------------------------------------
dnl Command line options
dnl ---------------------------------------------------------------------------

dnl -----------------------------------------
dnl --disable-debug (XXX: enabled by default)
dnl -----------------------------------------
AC_ARG_ENABLE([debug],
	      AC_HELP_STRING([--disable-debug],
			     [disable compiler debug information]),
	      [],
	      [enable_debug=yes;])

dnl ----------------------------
dnl --enable-debug-msgs
dnl ----------------------------
AC_ARG_ENABLE([debug-msgs],
	      AC_HELP_STRING([--enable-debug-msgs],
			     [enable debug messages]))

dnl ----------------------------
dnl --enable-debug-linker-maps
dnl ----------------------------
AC_ARG_ENABLE([debug-linker-maps],
	      AC_HELP_STRING([--enable-debug-linker-maps],
			     [enable linker map file output to stderr]))

dnl ----------------------------
dnl --enable-debug-fnames (GCC)
dnl ----------------------------
AC_ARG_ENABLE([debug-fnames],
	      AC_HELP_STRING([--enable-debug-fnames],
			     [enable function names in debug messages (requires GCC)]))

dnl ----------------------------
dnl --enable-stl-debug (GCC)
dnl ----------------------------
AC_ARG_ENABLE([stl-debug],
	      AC_HELP_STRING([--enable-stl-debug],
			     [enable STL debugging (requires GCC)]))

dnl ----------------------------
dnl --enable-optimize
dnl ----------------------------
AC_ARG_ENABLE([optimize],
	      AC_HELP_STRING([--enable-optimize],
			     [enable compiler optimizations]))

dnl ----------------------------
dnl --enable-profile
dnl ----------------------------
AC_ARG_ENABLE([profile],
	      AC_HELP_STRING([--enable-profile],
			     [enable profiling]))

dnl ----------------------------
dnl --enable-callback-debug
dnl ----------------------------
AC_ARG_ENABLE([callback-debug],
	      AC_HELP_STRING([--enable-callback-debug],
			     [enable callback debugging]),
	      AC_DEFINE(DEBUG_CALLBACKS, 1,
			[Define to enable callback debugging]))

dnl ----------------------------------------
dnl --disable-ipv6 (XXX: enabled by default)
dnl ----------------------------------------
AC_ARG_ENABLE([ipv6],
	      AC_HELP_STRING([--disable-ipv6],
			     [disable IPv6 support]),
	      [],
	      [enable_ipv6=yes;])

dnl ----------------------------
dnl --enable-sim
dnl ----------------------------
TRY_USE_SIM=false
AC_ARG_ENABLE([sim],
	      AC_HELP_STRING([--enable-sim],
			     [enable simulator (unimplemented)]),
if test "${enable_sim}" = "yes"; then
	TRY_USE_SIM=true;
else
	TRY_USE_SIM=false;
fi
)
AM_CONDITIONAL(TRY_USE_SIM, test "${enable_sim}" = "yes")

dnl ----------------------------------------------------------
dnl --disable-advanced-multicast-api (XXX: enabled by default)
dnl ----------------------------------------------------------
AC_ARG_ENABLE([advanced-multicast-api],
	      AC_HELP_STRING([--disable-advanced-multicast-api],
			     [disable use of the advanced multicast API]),
	      [],
	      [enable_advanced_multicast_api=yes;])

if test "${enable_advanced_multicast_api}" = "yes" ; then
	AC_DEFINE(ENABLE_ADVANCED_MULTICAST_API, 1,
		  [Define to 1 to enable advanced multicast API (if supported)])
fi

dnl ----------------------------
dnl --with-dmalloc
dnl ----------------------------
dnl XXX: XR_WITH_DMALLOC_DIR (a local modified version of AM_WITH_DMALLOC)
dnl is specified later, so it will overwrite -ld
builtin(include, config/dmalloc.m4)

dnl ----------------------------
dnl --with-openssl
dnl ----------------------------
AC_ARG_WITH([openssl],
	    AC_HELP_STRING([--with-openssl],
			   [specify path to OpenSSL installation root]),
	    [ xr_openssl_prefix="$withval" ])

dnl ----------------------------
dnl --with-snmp
dnl ----------------------------
AC_ARG_WITH(snmp,
	    AC_HELP_STRING([--with-snmp],
			   [build XORP MIBs for Net-SNMP]),
	 [ snmp="yes";
	   MIBS="mibs";
	   AC_CONFIG_SUBDIRS(mibs)
	 ],
	 [ snmp="no";
	   MIBS="";
	 ]
)
AC_SUBST(MIBS)


dnl ---------------------------------------------------------------------------
dnl Additional tool probes
dnl ---------------------------------------------------------------------------

dnl ----------------------------
dnl CPP GNU variable arguments
dnl ----------------------------

AC_CACHE_CHECK(
	[whether preprocessor supports GNU variadic macros],
	xorp_cv_cpp_gnu_va_args,
	AC_TRY_CPP(
		[
			#define SOME_PRINTF(args...) printf(args)
			SOME_PRINTF("hello %s %d", "world", '\n');
		],
		xorp_cv_cpp_gnu_va_args=yes,
		xorp_cv_cpp_gnu_va_args=no
	)
)
if test "${xorp_cv_cpp_gnu_va_args}" = "yes" ; then
	AC_DEFINE(CPP_SUPPORTS_GNU_VA_ARGS, 1,
 [Define to 1 if the preprocessor supports GNU style variadic macros])
fi

AC_CACHE_CHECK(
	[whether preprocessor supports C99 variadic macros],
	xorp_cv_cpp_c99_va_args,
	AC_TRY_CPP(
		[
			#define SOME_PRINTF(foo, ...) printf(args, __VA_ARGS__)
			SOME_PRINTF("hello %s %d", "world", '\n');
		],
		xorp_cv_cpp_c99_va_args=yes,
		xorp_cv_cpp_c99_va_args=no
	)
)
if test "${xorp_cv_cpp_c99_va_args}" = "yes" ; then
	AC_DEFINE(CPP_SUPPORTS_C99_VA_ARGS, 1,
 [Define to 1 if the preprocessor supports C99 style variadic macros])
fi

AC_CACHE_CHECK([if GNU make is installed], xorp_cv_gmake_name,
		for g in ${GMAKE} make gmake gnumake; do
		  if ($g -version) < /dev/null > /dev/null 2>&1 ; then
		    GMAKE_NAME="$g"
		    break
		  fi
		done
		xorp_cv_gmake_name=${GMAKE_NAME})

AC_CHECK_PROGS(PYTHON, python2.2 python2 python, "NO_PYTHON")
if test "${PYTHON}" != "NO_PYTHON" ; then
  AC_CACHE_CHECK(
    [python version 2.0 or above], xorp_python_good,
    changequote(<<,>>)dnl
    PY_VERSION=`$PYTHON -c 'import sys; print sys.version[[:3]]' | tr -cd '[[:digit:]]'`
    changequote([,])dnl
    if test 0${PY_VERSION} -ge 20 ; then
      xorp_python_good="yes"
    else
      xorp_python_good="no"
    fi
  )
fi

PY_BUILD="#"
if test "${xorp_python_good}" = "yes" ; then
   PY_BUILD=''
fi
AC_SUBST(PY_BUILD)
AC_SUBST(PYTHON)

dnl ----------------------------
dnl OpenSSL (Strictly OpenSSL MD5 implementation)
dnl ----------------------------

fail_openssl()
{
	echo "Could not find part of OpenSSL or one it's components in $1"
	echo "Use --with-openssl=DIR to specify OpenSSL installation root."
	exit 1
}

if test "${xr_openssl_prefix}" = "" ; then
# User has not specified an OpenSSL prefix, may be in cache, otherwise take
# a guess
    AC_CACHE_CHECK(
	[OpenSSL installation prefix],
	xr_cv_openssl_prefix,
	[
	    # Prefer base install over local install, assume base if not found
	    for xr_cv_openssl_prefix in /usr /usr/local /mingw /gnuwin32 /opt /usr ; do
		if test -d ${xr_cv_openssl_prefix}/include/openssl; then
		    break
		fi
	    done
	])
elif test "${xr_openssl_prefix}" != "${xr_cv_openssl_prefix}"; then

    # User has specified a new OpenSSL prefix, flush cache state of
    # header files and libraries that we care about to force a
    # re-check.
    unset ac_cv_header_openssl_md5_h
    unset ac_cv_lib_crypto_MD5_Init
    unset xr_cv_openssl_prefix

    # This cache check is doomed to fail because we unset variable,
    # but use it for consistent looking output.
    AC_CACHE_CHECK([OpenSSL installation prefix],
		   xr_cv_openssl_prefix,
		   xr_cv_openssl_prefix="${xr_openssl_prefix}")
fi

if test -d ${xr_cv_openssl_prefix}/include/openssl; then
    if test "${xr_cv_openssl_prefix}" != "/usr"; then
	CPPFLAGS="${CPPFLAGS} -I${xr_cv_openssl_prefix}/include"
	LIBS="${LIBS} -L${xr_cv_openssl_prefix} -L${xr_cv_openssl_prefix}/lib"
    fi
    AC_CHECK_HEADER(openssl/md5.h, , fail_openssl "${xr_cv_openssl_prefix}")
    AC_CHECK_LIB(crypto, MD5_Init, , fail_openssl "${xr_cv_openssl_prefix}")
else
    fail_openssl "${xr_cv_openssl_prefix}"
fi

dnl ---------------------------------------------------------------------------
dnl Check for libraries
dnl ---------------------------------------------------------------------------

dnl XXX Into C to avoid name mangling problems when checking libraries
AC_LANG_PUSH(C)

AC_SEARCH_LIBS(socket, socket)
AC_SEARCH_LIBS(inet_addr, nsl)
AC_SEARCH_LIBS(dlopen, dl)
AC_SEARCH_LIBS(hstrerror, resolv)
AC_SEARCH_LIBS(regcomp, pcreposix, , , [-lpcre])

dnl
dnl Select the optional CLI-related curses library
dnl
CLI_CURSES_LIB=""
AC_CHECK_LIB(curses, tigetstr, [CLI_CURSES_LIB="-lcurses"], [
  AC_CHECK_LIB(ncurses, tigetstr, [CLI_CURSES_LIB="-lncurses"], [
    AC_CHECK_LIB(curses, tgetstr, [CLI_CURSES_LIB="-lcurses"])
  ])
])
dnl XXX: This is a hack.
if test "${_USING_WINDOWS}" = "1"; then
	CLI_CURSES_LIB="-lpdcurses -ltermcap"
fi
AC_SUBST(CLI_CURSES_LIB)

dnl Replace main with a function in -lc:
dnl AC_CHECK_LIB(c, main)
dnl XXX: XR_WITH_DMALLOC_DIR must be after -lc otherwise it won't have efect
dnl XXX: XR_WITH_DMALLOC_DIR is a local modified version of AM_WITH_DMALLOC
XR_WITH_DMALLOC_DIR
dnl Replace main with a function in -lutil:
dnl AC_CHECK_LIB(util, main)
dnl AC_CHECK_LIB(crypt, crypt)

AC_LANG_POP(C)

dnl ---------------------------------------------------------------------------
dnl Check for header files and types
dnl ---------------------------------------------------------------------------

dnl
dnl Check for ANSI C Runtime Environment.
dnl
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h])

dnl
dnl Check for POSIX 1003.1 headers.
dnl
AC_CHECK_HEADERS([glob.h grp.h pwd.h regex.h termios.h sys/cdefs.h sys/errno.h sys/ioctl.h sys/mount.h sys/resource.h sys/select.h sys/signal.h sys/socket.h sys/stat.h sys/syslog.h sys/time.h sys/types.h sys/wait.h sys/uio.h sys/un.h])
AC_HEADER_TIME
AC_HEADER_STAT

dnl
dnl Check for the PCRE POSIX compatibility header (equivalent to regex.h).
dnl
AC_CHECK_HEADERS([pcreposix.h])

dnl
dnl Check for headers commonly found on UNIX systems (BSD, SYSV etc).
dnl
AC_CHECK_HEADERS([paths.h sysexits.h sys/param.h])
AC_HEADER_SYS_WAIT

dnl
dnl Check for typical BSD-like networking stack header files.
dnl
AC_CHECK_HEADERS([ifaddrs.h netdb.h arpa/inet.h arpa/telnet.h net/if.h net/if_arp.h net/if_dl.h net/if_types.h net/if_var.h net/route.h netinet/in.h netinet/in_systm.h netinet/in_var.h netinet/ip.h netinet/tcp.h sys/sockio.h])

dnl
dnl Check for typical BSD-like multicast header files.
dnl
AC_CHECK_HEADERS([netinet/igmp.h netinet/ip_mroute.h netinet/pim.h])

dnl
dnl Check for BSD-specific header files.
dnl
AC_CHECK_HEADERS([sys/linker.h sys/sysctl.h])

dnl
dnl Check for Linux-specific header files.
dnl
AC_CHECK_HEADERS([linux/rtnetlink.h linux/types.h])

dnl
dnl Check for Windows-specific header files.
dnl
AC_CHECK_HEADERS([windows.h winsock2.h ws2tcpip.h iphlpapi.h process.h])

dnl
dnl Check for headers where 'struct ether_addr' is typically defined.
dnl
AC_CHECK_HEADERS([sys/ethernet.h net/ethernet.h net/if_ether.h netinet/if_ether.h])
if test "${ac_cv_header_sys_ethernet_h}" = "yes"
then
	xr_cv_ether_header="sys/ethernet.h"
fi
if test "${ac_cv_header_net_ethernet_h}" = "yes"
then
	xr_cv_ether_header="net/ethernet.h"
fi
if test "${ac_cv_header_net_if_ether_h}" = "yes"
then
	xr_cv_ether_header="net/if_ether.h"
fi
if test "${ac_cv_header_netinet_if_ether_h}" = "yes"
then
	xr_cv_ether_header="netinet/if_ether.h"
fi

dnl
dnl Check for the ether_ntoa() function.
dnl
AC_CACHE_CHECK(for ether_ntoa in $xr_cv_ether_header,
	       xr_cv_have_ether_ntoa,
	       AC_EGREP_HEADER(ether_ntoa,
			       $xr_cv_ether_header,
			       xr_cv_have_ether_ntoa=yes,
			       xr_cv_have_ether_ntoa=no)
)
if test "${xr_cv_have_ether_ntoa}" = "no" ; then
   AC_DEFINE(NEED_ETHER_NTOA, 1,
		[Define to 1 if the C library is missing ether_ntoa(3)])
fi

dnl
dnl Check for the ether_aton() function.
dnl
AC_CACHE_CHECK(for ether_aton in $xr_cv_ether_header,
	       xr_cv_have_ether_aton,
	       AC_EGREP_HEADER(ether_aton,
			       $xr_cv_ether_header,
			       xr_cv_have_ether_aton=yes,
			       xr_cv_have_ether_aton=no)
)
if test "${xr_cv_have_ether_aton}" = "no" ; then
   AC_DEFINE(NEED_ETHER_ATON, 1,
		[Define to 1 if the C library is missing ether_aton(3)])
fi


dnl
dnl Tests for presentation and name lookup related functions.
dnl
builtin(include, config/acinet.m4)


dnl ---------------------------------------------------------------------------
dnl Check for standard size types.  The defaults are only valid on some
dnl systems so we hope that <inttypes.h> exists when they're wrong.
dnl ---------------------------------------------------------------------------

AC_CHECK_HEADERS([stdint.h inttypes.h])

dnl Some systems have these in <stdint.h>, just to be difficult...
AC_CACHE_CHECK(for uint8_t in <stdint.h>, xorp_cv_uint8_t_in_stdint_h,
        AC_EGREP_HEADER(uint8_t,
                        stdint.h,
                        xorp_cv_uint8_t_in_stdint_h=yes,
                        xorp_cv_uint8_t_in_stdint_h=no))
if test "${xorp_cv_uint8_t_in_stdint_h}" = "no"
then
         AC_CHECK_TYPE(uint8_t,  unsigned char)
fi

AC_CACHE_CHECK(for uint16_t in <stdint.h>, xorp_cv_uint16_t_in_stdint_h,
        AC_EGREP_HEADER(uint16_t,
                        stdint.h,
                        xorp_cv_uint16_t_in_stdint_h=yes,
                        xorp_cv_uint16_t_in_stdint_h=no))
if test "${xorp_cv_uint16_t_in_stdint_h}" = "no"
then
         AC_CHECK_TYPE(uint16_t,  unsigned short)
fi

AC_CACHE_CHECK(for uint32_t in <stdint.h>, xorp_cv_uint32_t_in_stdint_h,
        AC_EGREP_HEADER(uint32_t,
                        stdint.h,
                        xorp_cv_uint32_t_in_stdint_h=yes,
                        xorp_cv_uint32_t_in_stdint_h=no))
if test "${xorp_cv_uint32_t_in_stdint_h}" = "no"
then
         AC_CHECK_TYPE(uint32_t,  unsigned int)
fi

dnl ----------------------------
dnl TODO: the following two macros cause autoconf to complain.
dnl ----------------------------

AC_C_BIGENDIAN
AC_C_CHAR_UNSIGNED

dnl ---------------------------------------------------------------------------
dnl Check for typedefs and structures
dnl ---------------------------------------------------------------------------

AC_STRUCT_TM
XR_TYPE_SIG_T
AC_TYPE_MODE_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

AC_CHECK_TYPE([uid_t], ,
  [AC_DEFINE(NEED_UID_T, 1,
	     [Define to 1 if your system does not define uid_t])])

AC_CHECK_TYPE([ssize_t], ,
  [AC_DEFINE(NEED_SSIZE_T, 1,
	     [Define to 1 if your system does not define ssize_t])],
[
#include <sys/types.h>
])

dnl XXX move this above
AC_HEADER_TIOCGWINSZ

dnl XXX needed by cli code. C90
AC_FUNC_STRFTIME

dnl XXX needed by libxipc. C99
AC_FUNC_STRTOD

dnl XXX needed in many places it shouldn't be
AC_FUNC_FORK

dnl XXX needed in router manager
AC_CHECK_FUNCS([realpath])

dnl XXX
AC_CHECK_FUNCS([strsep])

dnl XXX
AC_CHECK_FUNCS([mkstemp])

dnl XXX We have a library check but we need to know if the function exists.
AC_CHECK_FUNCS([hstrerror])

dnl XXX needed in many places it shouldn't be
AC_CHECK_FUNCS([random], ,
  [AC_DEFINE(NEED_RANDOM, 1,
	     [Define to 1 if your system does not have modern random()])])

dnl
dnl XXX: This is a hack. Look for getopt in unistd.h by grepping for it.
dnl If it's not there, then look for the getopt.h header file.
dnl This assumes that the system's getopt declaration comes before the GNU one,
dnl if getopt.h is being picked up from a GNU C library include directory.
dnl This replaces the old NEED_GETOPT check which wasn't used.
dnl
AC_EGREP_HEADER(getopt, unistd.h, xr_cv_unistd_getopt=yes)
if test "${xr_cv_unistd_getopt}" = "yes"
then
    AC_DEFINE(HAVE_DECL_GETOPT, 1,
	      [Define to 1 if system getopt() is in unistd.h])
else
    AC_CHECK_HEADERS([getopt.h])
fi


dnl ---------------------------------------------------------------------------
dnl Check for library functions
dnl ---------------------------------------------------------------------------

AC_CHECK_FUNCS([strerror])
AC_CHECK_FUNCS([getifaddrs])
AC_CHECK_FUNCS([if_nametoindex])
AC_CHECK_FUNCS([if_indextoname])

dnl ---------------------------------------------------------------------------
dnl Check for system services
dnl ---------------------------------------------------------------------------

dnl
dnl Socket implementation checks
dnl
builtin(include, config/acsocket.m4)

dnl
dnl IPv4 stack checks
dnl
builtin(include, config/acipv4.m4)

dnl-----------------------------
dnl Check for IPv6
dnl-----------------------------
ipv6=no
AC_MSG_CHECKING(whether the system has IPv6 stack)
if test "${enable_ipv6}" = "no"; then
  AC_MSG_RESULT(disabled)
else
  AC_LANG_PUSH(C)
  AC_TRY_RUN([ /* AF_INET6 available check */
#include <stdlib.h>
#include <sys/types.h>
#include <sys/socket.h>
main()
{
 if (socket(AF_INET6, SOCK_STREAM, 0) < 0)
   return (1);
 else
   return (0);
}
],
  [AC_MSG_RESULT(yes)
   ipv6=yes],
  [AC_MSG_RESULT(no)],
  [AC_MSG_RESULT(no)])
  AC_LANG_POP(C)
fi

dnl
dnl IPv6 stack checks
dnl
if test "${ipv6}" = "yes"; then
  builtin(include, config/acipv6.m4)
fi

dnl
dnl Advanced Multicast API checks
dnl
builtin(include, config/acipmrt.m4)

dnl
dnl Firewall code checks
dnl
builtin(include, config/acfirewall.m4)

dnl
dnl Linux-specific checks
dnl
builtin(include, config/aclinux.m4)

dnl
dnl Interface configuration method checks
dnl
builtin(include, config/acifconf.m4)


dnl ---------------------------------------------------------------------------
dnl utils/flower_malloc related
dnl ---------------------------------------------------------------------------

dnl flower_malloc in utils is an interposer and is only known to
dnl work on systems supporting -shared.  AFAICT this needs some work for
dnl a real test.  As a stop gap, we assume that if we are on *BSD or Linux
dnl things work okay.  Only known failure case is OS X.

case "${host_os}" in
    *bsd*)  FLOWER_MALLOC=flower_malloc ;;
    linux*) FLOWER_MALLOC=flower_malloc ;;
    *) ;;
esac
AC_SUBST(FLOWER_MALLOC)

dnl ---------------------------------------------------------------------------
dnl Debug flags
dnl ---------------------------------------------------------------------------

if test "${enable_debug_msgs}" = "yes" ; then
	AC_DEFINE(DEBUG_LOGGING_GLOBAL, 1,
		[Define to 1 to enable globally debug messages output])
fi

dnl ---------------------------------------------------------------------------
dnl Check for compiler characteristics
dnl ---------------------------------------------------------------------------

dnl ----------------------------
dnl GCC-specific warning flags
dnl ----------------------------

if test "${GCC}" = "yes" ; then
	dnl
	dnl Filter-out the "-g" flag
	dnl
	CFLAGS=`echo $CFLAGS |  sed 's/-g//g'`
	CXXFLAGS=`echo $CXXFLAGS |  sed 's/-g//g'`

	dnl
	dnl If debugging is enabled, try to add the "-g" flag
	dnl
	if test "${enable_debug}" = "yes" ; then
		XR_TRY_ADD_CFLAGS("-g")
		XR_TRY_ADD_CXXFLAGS("-g")
	fi

	dnl
	dnl TODO: add -ansi -pedantic
	dnl XXX: Flag -Wtraditional is not recommended for C, because:
	dnl	- On FreeBSD-4.5 macros like IN_CLASSA(i) from netinet/in.h
	dnl	  create a warning for gcc 2.95.3
	dnl	- On Linux RedHat 7.2, #elif creates a warning for gcc 2.96
	dnl
	dnl XXX: Flag -Wnon-const-format is not included, because it appears
	dnl	 to exist only on earlier versions of gcc (e.g., gcc-2.95.x),
	dnl	 and only on FreeBSD, but not on Linux. In addition, this flag
	dnl	 appears to be problematic, because if it is enabled we cannot
	dnl	 use statements like "vfprintf(stderr, fmt, ap);" where "fmt"
	dnl	 is a variable (e.g., defined as "const char *").
	dnl
	dnl XXX: For extra debugging, the following g++ flag may be enabled
	dnl	 and added to CXXFLAGS:
	dnl	 -D_GLIBCXX_DEBUG
	dnl
	CPARANOIDFLAGS="-W -Wall -Wwrite-strings -Wbad-function-cast -Wmissing-prototypes -Wcast-qual -Wmissing-declarations -Werror -Wpointer-arith -Wcast-align -Wstrict-prototypes -Wnested-externs"
	CXXPARANOIDFLAGS="-W -Wall -Wwrite-strings -Wcast-qual -Werror -Wpointer-arith -Wcast-align -Wstrict-prototypes -Woverloaded-virtual -Wtraditional"
	XR_TRY_ADD_CFLAGS($CPARANOIDFLAGS)
	XR_TRY_ADD_CXXFLAGS($CXXPARANOIDFLAGS)
	XR_TRY_ADD_CXXFLAGS("-ftemplate-depth-22")

	dnl
	dnl Try to add the -pipe option, which can significantly speed up
	dnl compile runs by avoiding the use of temporary files.
	dnl
	XR_TRY_ADD_CFLAGS("-pipe")
	XR_TRY_ADD_CXXFLAGS("-pipe")

	dnl
	dnl Default compiler flags have optimization turned on which
	dnl can cause helpful debugging info to be optimized out.
	dnl
	if test "${enable_optimize}" != "yes" ; then
		dnl Could just append -O0, but two -O args are icky
		dnl We make assumption change is O2 because extended regexps
		dnl are not portable...
		changequote(<<, >>)dnl preserve braces in sed expressions
		CFLAGS=`echo $CFLAGS | sed 's/-O2//g'`
		CXXFLAGS=`echo $CXXFLAGS | sed 's/-O2//g'`
		changequote([, ])dnl
	fi
	if test "${enable_profile}" = "yes" ; then
		CFLAGS="${CFLAGS} -pg"
		CXXFLAGS="${CXXFLAGS} -pg"
	fi
	if test "${enable_debug_fnames}" = "yes" ; then
		AC_DEFINE(DEBUG_PRINT_FUNCTION_NAME, 1,
			[Define to 1 to enable printing function name in debug messages])
	fi
	if test "${enable_stl_debug}" = "yes" ; then
		CXXFLAGS="${CXXFLAGS} -D_GLIBCXX_DEBUG -D_GLIBCXX_DEBUG_PEDANTIC"
	fi

	dnl
	dnl Conditionally add the user-specified flags that must be after
	dnl all other compiler flags.
	dnl
	dnl Those are specified in the "CFLAGS_END" and "CXXFLAGS_END"
	dnl environmental variables (for the C and C++ compiler respectively).
	dnl
	XR_TRY_ADD_CFLAGS($CFLAGS_END)
	XR_TRY_ADD_CXXFLAGS($CXXFLAGS_END)
fi

dnl
dnl Win32/64 specific flags go here.
dnl
if test "${_USING_WINDOWS}" = "1"; then
	LDFLAGS="${LDFLAGS} -mconsole"
	LIBS="${LIBS} -lws2_32 -liphlpapi"
fi

dnl
dnl Deal with linker file generation last (it might not work.)
dnl
if test "${enable_debug_linker_maps}" = "yes" ; then
	LDFLAGS="--Wl,-M --Wl,--cref ${LDFLAGS}"
fi

dnl ---------------------------------------------------------------------------
dnl Hack to stop building Moy OSPF unconditionally until new code is merged.
dnl ---------------------------------------------------------------------------

OSPFD=""
OSPFD_OS="unsupported"
AC_SUBST(OSPFD)
AC_SUBST(OSPFD_OS)

dnl ---------------------------------------------------------------------------
dnl Add files to under-go substitution here.
dnl ---------------------------------------------------------------------------

AC_OUTPUT(
	Makefile
	MakefileRootCheck
	bgp/Makefile
	bgp/harness/Makefile
	bgp/tools/Makefile
	cli/Makefile
	cli/tools/Makefile
	contrib/Makefile
	docs/Makefile
	docs/bgp/Makefile
	docs/design_arch/Makefile
	docs/fea/Makefile
	docs/libxipc/Makefile
	docs/libxorp/Makefile
	docs/mfea/Makefile
	docs/mld6igmp/Makefile
	docs/multicast/Makefile
	docs/pim/Makefile
	docs/pim_testsuite/Makefile
	docs/rib/Makefile
	docs/rtrmgr/Makefile
	docs/slides/Makefile
	docs/slides/status_2004_02/Makefile
	docs/snmp/Makefile
	docs/test_harness/Makefile
	docs/user_manual/Makefile
	docs/xorpdev_101/Makefile
	etc/Makefile
	etc/templates/Makefile
	fea/Makefile
	fea/MakefileRootCheck
	fea/tools/Makefile
	fib2mrib/Makefile
	libcomm/Makefile
	libfeaclient/Makefile
	libproto/Makefile
	libxipc/Makefile
	libxorp/Makefile
	mld6igmp/Makefile
	mrt/Makefile
	pim/Makefile
	policy/Makefile
	policy/backend/Makefile
	policy/common/Makefile
	policy/test/Makefile
	rib/Makefile
	rib/tools/Makefile
	rip/Makefile
	rip/tools/Makefile
	rtrmgr/Makefile
	static_routes/Makefile
	utils/Makefile
	utils/flower_malloc/Makefile
	xrl/Makefile
	xrl/interfaces/Makefile
	xrl/targets/Makefile
	xrl/tests/Makefile
,
	echo timestamp > stamp-h
)


dnl ------------------------
dnl Print the compiler flags
dnl ------------------------
echo "Compiler C flags = $CFLAGS"
echo "Compiler C++ flags = $CXXFLAGS"
echo "C preprocessor flags = $CPPFLAGS"
echo "Linker flags = $LDFLAGS"


dnl ---------------------------------------------------------------------------
dnl Warn if default make is not gmake
dnl ---------------------------------------------------------------------------

if test -z "${xorp_cv_gmake_name}" ; then
   echo "=============================================================================="
   echo "GNU make was not found during configure process and is essential for building"
   echo "XORP.  If you believe this is an error, please let feedback@xorp.org know."
   echo "=============================================================================="
   echo
elif test "${xorp_cv_gmake_name}" != "make" ; then
   echo
   echo "=============================================================================="
   echo "GNU make is required when building XORP.  This appears to be installed as"
   echo "\"${xorp_cv_gmake_name}\".  If this does not work, please let feedback@xorp.org know."
   echo "=============================================================================="
   echo
fi

dnl ---------------------------------------------------------------------------
dnl END
dnl ---------------------------------------------------------------------------
